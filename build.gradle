// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'com.github.ben-manes.versions'

ext {
    // https://developer.android.com/jetpack/androidx/releases/recyclerview
    recyclerviewVersion = '1.3.2'
    // https://developer.android.com/jetpack/androidx/releases/preference
    preferenceVersion = '1.2.1'
    // https://github.com/material-components/material-components-android/releases
    // 1.10+: compileSdkVersion 34
    materialVersion = '1.9.0'
    // https://developer.android.com/jetpack/androidx/releases/lifecycle
    lifecycleVersion = '2.6.2'
    // https://developer.android.com/jetpack/androidx/releases/leanback
    // 1.2.0-alpha03+: compileSdkVersion 34
    leanbackVersion = '1.2.0-alpha02'
    // https://developer.android.com/jetpack/androidx/releases/appcompat
    // 1.7.0-alpha03+: compileSdkVersion 34
    appcompatVersion = '1.6.1'
    // https://developer.android.com/jetpack/androidx/releases/annotation
    annotationVersion = '1.7.0'
    // https://github.com/google/desugar_jdk_libs/blob/master/CHANGELOG.md
    desugarVersion = '2.0.4'
}

buildscript {
    ext.kotlin_version = '1.9.20'
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.4'
        // https://github.com/KeepSafe/dexcount-gradle-plugin
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:4.0.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
        // https://github.com/ben-manes/gradle-versions-plugin
        classpath "com.github.ben-manes:gradle-versions-plugin:0.50.0"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' } // for AwesomeQRCode
    }
    project.ext {
        // My test v15 device crashes (probably material related/HW acceleration), so I've given up on ICS
        minSdkVersion = 16
        targetSdkVersion = 33
        compileSdkVersion = 33
    }
}

