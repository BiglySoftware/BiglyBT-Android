apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    dexOptions {
        preDexLibraries = false
        javaMaxHeapSize "4g"
    }

    defaultConfig {
        applicationId "com.biglybt.android.client"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 10
        versionName "1.1.1"
        multiDexEnabled true
    }
    buildTypes {
        debug {
            debuggable true
            versionNameSuffix ' Alpha'
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt', 'proguard-project-debug.txt'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt', 'proguard-project-release.txt'
            debuggable false
        }
    }
    defaultConfig {
        vectorDrawables.useSupportLibrary = true
    }
    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
    }

    flavorDimensions "coreD", "gaD"

    productFlavors {
        coreFlavor {
            dimension "coreD"
        }

        noCoreFlavor {
            dimension "coreD"
        }

        gaFlavor {
            dimension "gaD"
        }

        nogaFlavor {
            dimension "gaD"
        }
    }

    sourceSets {
        coreFlavor.java.srcDirs = ['src/coreFlavor/java']
        noCoreFlavor.java.srcDirs = ['src/noCoreFlavor/java']
        gaFlavor.java.srcDirs = ['src/gaFlavor/java']
        nogaFlavor.java.srcDirs = ['src/nogaFlavor/java']
    }
}

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}


dependencies {
    compileOnly 'org.jetbrains:annotations:13.0'
    implementation "com.android.support:support-annotations:$versions.support"
    implementation project(':pagerSlidingTabStrip')
    implementation project(':recyclerview-fastscroll')
    gaFlavorImplementation "com.google.android.gms:play-services-analytics:$versions.gms"
    gaFlavorImplementation 'com.android.billingclient:billing:1.0'
    coreFlavorImplementation fileTree(dir: 'libs', include: ['*.jar'])
    coreFlavorImplementation 'dnsjava:dnsjava:2.1.7'
    implementation "com.android.support:support-v4:$versions.support"
    implementation "com.android.support:leanback-v17:$versions.support"
    implementation "com.android.support:appcompat-v7:$versions.support"
    implementation "com.android.support:recyclerview-v7:$versions.support"
    implementation "com.android.support:design:$versions.support"
    implementation "com.android.support:preference-v7:$versions.support"
    implementation "com.android.support:preference-v14:$versions.support"
    implementation "com.android.support:preference-leanback-v17:$versions.support"
    // Note: There's more active forks of materialedittext which remove
    // nineoldandroids, but their minSDK is beyond ours
    implementation 'com.rengwuxian.materialedittext:library:2.1.4'
    implementation 'com.alibaba:fastjson:1.1.64.android'
    implementation 'jcifs:jcifs:1.3.17'
    implementation 'com.android.support:multidex:1.0.2'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'divstar:ico4a:v1.0'
    implementation 'com.squareup.okhttp3:okhttp:3.9.0'
    implementation 'com.squareup.okio:okio:1.13.0'
    implementation 'net.i2p.android:helper:0.9.4'
    implementation 'com.github.SumiMakito:AwesomeQRCode:1.0.6'
    implementation 'com.google.android:flexbox:0.3.1'
    implementation 'com.jaredrummler:android-device-names:1.1.5'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0-beta3'
}

afterEvaluate {
    tasks.matching {
        it.name.startsWith('dex')
    }.each { dx ->
        if (dx.additionalParameters == null) {
            dx.additionalParameters = []
        }
        dx.additionalParameters += "--set-max-idx-number=53000" // default 60000
    }
}

apply plugin: 'com.getkeepsafe.dexcount'

if (getGradle().getStartParameter().getTaskRequests()
        .toString().toLowerCase().contains("flavorgaflavor")){
    // For google-services.json, use https://developers.google.com/analytics/devguides/collection/android/v4/
    if (file("./google-services.json").exists())
        apply plugin: 'com.google.gms.google-services'
}
