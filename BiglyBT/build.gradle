apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    dexOptions {
        preDexLibraries = false
        javaMaxHeapSize "4g"
    }
    defaultConfig {
        applicationId "com.biglybt.android.client"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 33
        versionName "1.2.0"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        debug {
            debuggable true
            versionNameSuffix ' Debug'
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt', 'proguard-project-debug.txt'
        }
        alpha {
            debuggable false
            versionNameSuffix ' Alpha'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt', 'proguard-project-release.txt'
            minifyEnabled true
            useProguard true
            matchingFallbacks = ['release']
        }
        beta {
            debuggable false
            versionNameSuffix ' beta'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt', 'proguard-project-release.txt'
            minifyEnabled true
            useProguard true
            matchingFallbacks = ['release']
        }
        release {
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt', 'proguard-project-release.txt'
            minifyEnabled true
            useProguard true
        }
    }
    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'org/json/simple/*.txt'
        exclude 'com/aelitis/plugins/rcmplugin/skins'
    }
    flavorDimensions "coreD", "gaD"
    productFlavors {
        coreFlavor {
            dimension "coreD"
        }

        noCoreFlavor {
            dimension "coreD"
        }

        googleFlavor {
            dimension "gaD"
        }

        fossFlavor {
            dimension "gaD"
        }
    }
    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res',
                    'src/main/res.torrentlist',
                    'src/main/res.torrentdetails',
                    'src/main/res.openoptions',
                    'src/main/res.metasearch',
                    'src/main/res.subscription',
                    'src/main/res.sidelist',
                    'src/main/res.drawables',
                    'src/main/res.lang',
                    'src/main/res.rcm'
            ]
        }
        coreFlavor.java.srcDirs = ['src/coreFlavor/java']
        noCoreFlavor.java.srcDirs = ['src/noCoreFlavor/java']
        googleFlavor.java.srcDirs = ['src/googleFlavor/java']
        fossFlavor.java.srcDirs = ['src/fossFlavor/java']
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}


dependencies {
    compileOnly 'org.jetbrains:annotations:16.0.3'

    coreFlavorImplementation fileTree(dir: 'libs', include: ['*.jar'])

    // https://sourceforge.net/projects/dnsjava/
    coreFlavorImplementation 'dnsjava:dnsjava:2.1.8'

    // For Tips $
    googleFlavorImplementation 'com.android.billingclient:billing:1.2'

    implementation "com.android.support:support-annotations:$versions.support"
    implementation "com.android.support:support-v4:$versions.support"
    implementation "com.android.support:leanback-v17:$versions.support"
    implementation "com.android.support:appcompat-v7:$versions.support"
    implementation "com.android.support:recyclerview-v7:$versions.support"
    implementation "com.android.support:design:$versions.support"
    implementation "com.android.support:preference-v7:$versions.support"
    implementation "com.android.support:preference-v14:$versions.support"
    implementation "com.android.support:preference-leanback-v17:$versions.support"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    // Note: There's more active forks of materialedittext which remove
    // nineoldandroids, but their minSDK is beyond ours
    implementation 'com.rengwuxian.materialedittext:library:2.1.4'

    // https://github.com/alibaba/fastjson
    implementation 'com.alibaba:fastjson:1.1.68.android'

    // https://jcifs.samba.org/
    implementation 'jcifs:jcifs:1.3.17'
    implementation 'com.android.support:multidex:1.0.3'

    // https://github.com/square/picasso
    // Note: 2.71828 (2018-03-07) is not compatible with other 2.x releases
    implementation 'com.squareup.picasso:picasso:2.5.2'

    // https://github.com/divStar/ico4a
    implementation 'divstar:ico4a:v1.0'

    // https://github.com/square/okhttp
    implementation 'com.squareup.okhttp3:okhttp:3.12.0'

    // https://github.com/square/okio
    // Note: 2.x is Kotlin
    implementation 'com.squareup.okio:okio:1.16.0'

    // https://mvnrepository.com/artifact/net.i2p.android/helper
    implementation 'net.i2p.android:helper:0.9.5'

    // https://github.com/SumiMakito/AwesomeQRCode
    // Note: 1.2+ is Kotlin
    implementation 'com.github.SumiMakito:AwesomeQRCode:1.0.6'

    // https://github.com/google/flexbox-layout
    // Note: 1.1.0+ is androidx
    implementation 'com.google.android:flexbox:1.0.0'

    // https://github.com/jaredrummler/AndroidDeviceNames
    // Note: 1.1.8+ is androidx
    implementation 'com.jaredrummler:android-device-names:1.1.7'

    // https://github.com/inloop/UpdatableFragmentStatePagerAdapter
    // implementation 'eu.inloop:pageradapter:0.1.0'

    // https://github.com/grandcentrix/tray
    // grandcentrix's implementation has ConcurrentModificationException errors
    // For now, use https://github.com/jiechic/tray/tree/version which fixes that
    //implementation 'net.grandcentrix.tray:tray:0.12.0'
    implementation project(':pagerSlidingTabStrip')
    implementation project(':recyclerview-fastscroll')
    implementation project(':jiechic-tray')
    // https://github.com/BiglySoftware/Android-Toggle-Switch which is
    // https://github.com/bkhezry/Android-Toggle-Switch which is
    // https://github.com/llollox/Android-Toggle-Switch
    implementation project(':androidtoggleswitch')
}

afterEvaluate {
    tasks.matching {
        it.name.startsWith('dex')
    }.each { dx ->
        if (dx.additionalParameters == null) {
            dx.additionalParameters = []
        }
        dx.additionalParameters += "--set-max-idx-number=53000" // default 60000
    }
}

apply plugin: 'com.getkeepsafe.dexcount'
