apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    dexOptions {
        preDexLibraries = false
        javaMaxHeapSize "4g"
    }

    defaultConfig {
        applicationId "com.biglybt.android.client"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 3
        versionName "1.0"
        multiDexEnabled true
    }
    buildTypes {
        debug {
            debuggable true
            versionNameSuffix ' Alpha'
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt', 'proguard-project-debug.txt'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt', 'proguard-project-release.txt'
            debuggable false
        }
    }
    defaultConfig {
        vectorDrawables.useSupportLibrary = true
    }
    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
    }

    flavorDimensions "coreD", "gaD"

    productFlavors {
        coreFlavor {
            dimension "coreD"
        }

        noCoreFlavor {
            dimension "coreD"
        }

        gaFlavor {
            dimension "gaD"
        }

        nogaFlavor {
            dimension "gaD"
        }
    }

    sourceSets {
        coreFlavor.java.srcDirs = ['src/coreFlavor/java']
        noCoreFlavor.java.srcDirs = ['src/noCoreFlavor/java']
        gaFlavor.java.srcDirs = ['src/gaFlavor/java']
        nogaFlavor.java.srcDirs = ['src/nogaFlavor/java']
    }
}

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}


dependencies {
    provided 'org.jetbrains:annotations:13.0'
    compile 'com.android.support:support-annotations:25.4.0'
    compile project(':pagerSlidingTabStrip')
    compile project(':recyclerview-fastscroll')
    compile project(':android-numberpicker')
    // 10.0.x is the last version supporting API < 11
    gaFlavorCompile 'com.google.android.gms:play-services-analytics:10.0.1'
    coreFlavorCompile fileTree(dir: 'libs', include: ['*.jar'])
    coreFlavorCompile 'dnsjava:dnsjava:2.1.7'
    compile 'com.android.support:support-v4:25.4.0'
    compile 'com.android.support:leanback-v17:25.4.0'
    compile 'com.android.support:appcompat-v7:25.4.0'
    compile 'com.android.support:recyclerview-v7:25.4.0'
    compile 'com.android.support:design:25.4.0'
    compile 'com.rengwuxian.materialedittext:library:2.1.4'
    compile 'com.alibaba:fastjson:1.1.56.android'
    compile 'jcifs:jcifs:1.3.17'
    compile 'com.android.support:multidex:1.0.2'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'divstar:ico4a:v1.0'
    // We use libs/okhttp-3.5.0.jar without a HttpHeaders.class so that we
    // can overwrite it to handle a bug in the client's Vuze core.  When the
    // majority of users are on a fixed version of Vuze, we can use maven
    //compile 'com.squareup.okhttp3:okhttp:3.5.0'
    compile files('libs/okhttp-3.5.0.jar')
    compile 'com.squareup.okio:okio:1.11.0'
    compile 'net.i2p.android:helper:0.9'
}

afterEvaluate {
    tasks.matching {
        it.name.startsWith('dex')
    }.each { dx ->
        if (dx.additionalParameters == null) {
            dx.additionalParameters = []
        }
        dx.additionalParameters += "--set-max-idx-number=53000" // default 60000
    }
}

apply plugin: 'com.getkeepsafe.dexcount'
// For google-services.json, use https://developers.google.com/analytics/devguides/collection/android/v4/
apply plugin: 'com.google.gms.google-services'